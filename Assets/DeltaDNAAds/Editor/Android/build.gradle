apply plugin: 'com.android.application'

def notifications = hasProperty('notifications')
def smartads = hasProperty('smartads')
def networks = hasProperty('networks') ? networks.split(',') : []
def network = { network ->
    if (network in networks) {
        dependencies.compile("com.deltadna.android:deltadna-smartads-provider-${network}:1.0.0-SNAPSHOT")
    }
}

buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://jcenter.bintray.com' }
    maven { url 'http://edinlin01:8082/artifactory/libs-snapshot-local' }
    maven { url "http://deltadna.bintray.com/android" }
}

android {
    buildToolsVersion '23.0.2'
    compileSdkVersion 23
}

dependencies {
    if (notifications) {
        compile 'com.deltadna.android:deltadna-sdk:4.0.0-SNAPSHOT'
    }
    if (smartads) {
        compile 'com.deltadna.android:deltadna-smartads-core:1.0.0-SNAPSHOT'
    }

    network('admob')
    network('amazon')
    network('mobfox')
    network('mopub')
    network('adcolony')
    network('chartboost')
    network('flurry')
    network('inmobi')
    network('unity')
    network('vungle')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

task clean(type: Delete) {
    delete '../../Plugins/Android'
}

task download(type: Copy) {
    from configurations.compile
    into '../../Plugins/Android'
}
